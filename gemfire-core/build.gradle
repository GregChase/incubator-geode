sourceSets {
  jca {
    compileClasspath += configurations.compile
    runtimeClasspath += configurations.runtime
  }
}

configurations {
  //declaring new configuration that will be used to associate with artifacts
  archives
}

dependencies {
  // Source Dependencies
  // External 
  provided files("${System.getProperty('java.home')}/../lib/tools.jar")
  compile 'antlr:antlr:' + property("antlr.version")
  compile 'com.fasterxml.jackson.core:jackson-annotations:' + property("jackson.version")
  compile 'com.fasterxml.jackson.core:jackson-core:' + property("jackson.version")
  compile 'com.fasterxml.jackson.core:jackson-databind:' + property("jackson.version")
  compile 'com.google.code.findbugs:annotations:' + property("annotations.version")
  provided 'com.google.guava:guava:' + property("guava.version")
  compile 'commons-io:commons-io:' + property("commons-io.version")
  provided 'commons-lang:commons-lang:' + property("commons-lang.version")
  compile 'commons-logging:commons-logging:' + property("commons-logging.version")
  compile 'commons-modeler:commons-modeler:' + property("commons-modeler.version")
  compile 'io.netty:netty-all:' + property("netty-all.version")
  compile 'it.unimi.dsi:fastutil:' + property("fastutil.version")
  compile 'javax.activation:activation:' + property("activation.version")
  compile 'javax.mail:javax.mail-api:' + property("javax.mail-api.version")
  compile 'javax.resource:javax.resource-api:' + property("javax.resource-api.version")
  compile 'javax.servlet:javax.servlet-api:' + property("javax.servlet-api.version")
  compile 'javax.transaction:javax.transaction-api:' + property("javax.transaction-api.version")
  compile 'mx4j:mx4j:' + property("mx4j.version")
  compile 'mx4j:mx4j-remote:' + property("mx4j.version")
  compile 'mx4j:mx4j-tools:' + property("mx4j.version")
  compile 'net.java.dev.jna:jna:' + property("jna.version")
  compile 'net.sourceforge.jline:jline:' + property("jline.version")
  provided 'org.apache.hadoop:hadoop-common:' + property("hadoop.version")
  provided 'org.apache.hadoop:hadoop-annotations:' + property("hadoop.version")
  provided 'org.apache.hadoop:hadoop-hdfs:' + property("hadoop.version")
  provided 'org.apache.hadoop:hadoop-mapreduce-client-core:' + property("hadoop.version")
  compile 'org.apache.hbase:hbase:' + property("hbase.version")
  compile 'org.apache.logging.log4j:log4j-api:' + property("log4j.version")
  compile 'org.apache.logging.log4j:log4j-core:' + property("log4j.version")
  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:' + property("log4j.version")
  runtime 'org.apache.logging.log4j:log4j-jcl:' + property("log4j.version")
  runtime 'org.apache.logging.log4j:log4j-jul:' + property("log4j.version")
  compile 'org.apache.lucene:lucene-analyzers-common:' + property("lucene.version")
  compile 'org.apache.lucene:lucene-core:' + property("lucene.version")
  compile 'org.apache.lucene:lucene-queries:' + property("lucene.version")
  compile 'org.apache.lucene:lucene-queryparser:' + property("lucene.version")
  compile 'org.eclipse.jetty:jetty-http:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-io:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-security:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-server:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-servlet:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-util:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-webapp:' + property("jetty.version")
  compile 'org.eclipse.jetty:jetty-xml:' + property("jetty.version")
  compile 'org.fusesource.jansi:jansi:' + property("jansi.version")
  compile 'org.slf4j:slf4j-api:' + property("slf4j-api.version")
  compile 'org.springframework.data:spring-data-commons:' + property("spring-data-commons.version")
  provided 'org.springframework.data:spring-data-gemfire:' + property("spring-data-gemfire.version")
  compile 'org.springframework:spring-tx:' + property("springframework.version")
  compile 'org.springframework.shell:spring-shell:' + property("spring-shell.version")
  compile 'org.xerial.snappy:snappy-java:' + property("snappy-java.version")
  compile 'org.apache.hbase:hbase:' + property("hbase.version")

  // Internal
  compile project(':gemfire-jgroups')
  compile project(':gemfire-joptsimple')
  compile project(':gemfire-json')

  jcaCompile sourceSets.main.output

  provided project(path: ':gemfire-junit', configuration: 'testOutput')

  // Test Dependencies
  // External
  testCompile 'org.apache.bcel:bcel:' + property("bcel.version")
  testRuntime 'org.apache.derby:derby:' + property("derby.version")
  testRuntime 'org.apache.hadoop:hadoop-auth:' + property("hadoop.version")
  testRuntime 'commons-collections:commons-collections:' + property("commons-collections.version")
  testRuntime 'commons-configuration:commons-configuration:' + property("commons-configuration.version")
  testRuntime 'commons-io:commons-io:' + property("commons-io.version")
  //testRuntime 'log4j:log4j:' + property("log4j.version")
  testCompile 'net.spy:spymemcached:' + property("spymemcached.version")
  testCompile 'redis.clients:jedis:' + property("jedis.version")

}

def generatedResources = "$buildDir/generated-resources/main"

sourceSets {
  main {
    output.dir(generatedResources, builtBy: 'createVersionPropertiesFile')
  }
}
 
// Creates the version properties file and writes it to the classes dir
task createVersionPropertiesFile {
  def propertiesFile = file(generatedResources + "/com/gemstone/gemfire/internal/GemFireVersion.properties");
  outputs.file propertiesFile
  inputs.dir compileJava.destinationDir

  doLast {
    new ByteArrayOutputStream().withStream { gitWorkspaceStream ->
      def result = exec {
        workingDir = "${projectDir}"
        standardOutput = gitWorkspaceStream
        ignoreExitValue = true
        executable = "git"
        args = ['rev-parse', '--is-inside-work-tree']
      }
      ext.isGitWorkspace = gitWorkspaceStream.toString()
      ext.isGitWorkspace = ext.isGitWorkspace.trim()
    }

    if ( isGitWorkspace.equalsIgnoreCase('true') ) {
      new ByteArrayOutputStream().withStream { gitBranchStream ->
        def result = exec {
          workingDir = "${projectDir}"
          standardOutput = gitBranchStream
          executable = "git"
          args = ['rev-parse', '--abbrev-ref', 'HEAD']
        }
        ext.gitBranchString = gitBranchStream.toString()
        ext.gitBranch = ext.gitBranchString.trim()
      }

      new ByteArrayOutputStream().withStream { commitStream ->
        def result = exec {
          workingDir = "${projectDir}"
          standardOutput = commitStream
          executable = "git"
          args = ['rev-parse', 'HEAD']
        }
        ext.commitIdString = commitStream.toString()
        ext.commitId = ext.commitIdString.trim()
      }

      new ByteArrayOutputStream().withStream { sourceDateStream ->
        def result = exec {
          workingDir = "${projectDir}"
          standardOutput = sourceDateStream
          executable = "git"
          args = ['show', '-s', '--format=%ci', "${ext.commitId}"]
        }
        ext.sourceDateString = sourceDateStream.toString()
        ext.sourceDate = ext.sourceDateString.trim()
      }
    }
    else {
      // Not in SCM workspace, use default values
      ext.gitBranch = 'UNKNOWN'
      ext.commitId = 'UNKNOWN'
      ext.sourceDate = new Date().format('yyyy-MM-dd HH:mm:ss Z')
    }

    ext.osArch = System.getProperty('os.arch')
    ext.osName = System.getProperty('os.name')
    ext.osVersion = System.getProperty('os.version')
    ext.buildDate = new Date().format('yyyy-MM-dd HH:mm:ss Z')
    ext.buildNumber = new Date().format('MMddyy')
    ext.jdkVersion = System.getProperty('java.version')

    def props = [
      "Product-Name"      : "Apache Geode (incubating)",
      "Product-Version"   : version,
      "Build-Id"          : System.env.USER + ' ' + ext.buildNumber,
      "Build-Date"        : ext.buildDate,
      "Build-Platform"    : ext.osName + ' ' + ext.osVersion + ' ' + ext.osArch,
      "Build-Java-Version": ext.jdkVersion,
      "Source-Date"       : ext.sourceDate,
      "Source-Revision"   : ext.commitId,
      "Source-Repository" : ext.gitBranch
    ] as Properties

    propertiesFile.getParentFile().mkdirs();
    new FileOutputStream(propertiesFile).withStream { fos ->
      props.store(fos, '')
    }
  }
}

jar {

  from sourceSets.main.output
  from sourceSets.jca.output
  
  exclude 'com/gemstone/gemfire/management/internal/web/**'
  exclude 'com/gemstone/gemfire/internal/i18n/StringIdResourceBundle_ja.txt'
  exclude 'com/gemstone/gemfire/admin/doc-files/ds4_0.dtd'
}

task webJar (type: Jar, dependsOn: classes) {
  description 'Assembles the jar archive containing the gemfire management web classes.'
  from sourceSets.main.output
  baseName 'gemfire-web'
  include 'com/gemstone/gemfire/management/internal/web/**'
}

task raJar (type: Jar, dependsOn: classes) {
  description 'Assembles the jar archive that contains the JCA classes'
  from sourceSets.jca.output
  exclude 'com/gemstone/gemfire/ra/**'
  archiveName 'ra.jar'
}

task jcaJar (type: Jar, dependsOn: raJar) {
  description 'Assembles the jar archive that contains the JCA bundle'
  baseName 'gemfire-jca'
  extension 'rar'
  metaInf { from 'src/jca/ra.xml' }
  from raJar.archivePath
}

artifacts {
  archives webJar, raJar, jcaJar
}

configurations {
  classesOutput {
    extendsFrom compile
    description  'a dependency that exposes the compiled classes'
  }
}

dependencies {
  classesOutput sourceSets.main.output
}
